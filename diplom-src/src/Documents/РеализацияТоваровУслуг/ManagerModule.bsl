
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

//+ВКМ	_12.08.2024
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт об оказанных услугах
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктВыполненныхРабот";
	КомандаПечати.Представление = НСтр("ru = 'Акт выполненных работ'");
	КомандаПечати.Порядок = 5;
	
КонецПроцедуры 

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктВыполненныхРабот");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ВКМ_АктВыполненныхРабот(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт выполненных работ'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ВКМ_ПФ_MXL_АктВыполненныхРабот";
	КонецЕсли;

КонецПроцедуры
//-ВКМ	_12.08.2024

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//+ВКМ	_12.08.2024
Функция ВКМ_АктВыполненныхРабот(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ВКМ_ПФ_MXL_АктВыполненныхРабот");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	               |	РеализацияТоваровУслуг.Номер КАК Номер,
	               |	РеализацияТоваровУслуг.Дата КАК Дата,
	               |	РеализацияТоваровУслуг.Организация.Представление КАК Организация,
	               |	РеализацияТоваровУслуг.Контрагент.Представление КАК Контрагент,
	               |	РеализацияТоваровУслуг.Договор.Представление КАК Договор,
	               |	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	               |	РеализацияТоваровУслуг.Товары.(
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма,
	               |		Номенклатура.Представление КАК Номенклатура
	               |	) КАК Товары,
	               |	РеализацияТоваровУслуг.Услуги.(
	               |		Номенклатура.Представление КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Ссылка В(&Ссылка)";
	
	Запрос.УстановитьПараметр("Ссылка", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");  
	ОбластьШапкаТаблица = Макет.ПолучитьОбласть("ШапкаТаблица");
	ОбластьСтрокиТаблицы = Макет.ПолучитьОбласть("СтрокиТаблицы");
	ОбластьИтог = Макет.ПолучитьОбласть("Итог");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал"); 
	
	ТабличныйДокумент.Очистить();
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл 
		
		Если ВставлятьРазделительСтраниц Тогда 
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ОбластьЗаголовок.Параметры.Номер = ВКМ_УбратьНули(Выборка.Номер);
		ОбластьЗаголовок.Параметры.Дата = Формат(Выборка.Дата, "ДЛФ=DD");
		
		// заголовок
		ТабличныйДокумент.Вывести(ОбластьЗаголовок); 
		
		// шапка
		ОбластьШапка.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(ОбластьШапка, Выборка.Уровень());
		
		// шапка таблица
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблица);
		ИтогТовары = 0;
		ИтогУслуги = 0;
		НомерСтроки = 0;
		
		// строки таблицы
		ВыборкаТовары = Выборка.Товары.Выбрать();
		
		Пока ВыборкаТовары.Следующий() Цикл

			НомерСтроки = НомерСтроки + 1;
			ОбластьСтрокиТаблицы.Параметры.НомерСтроки = НомерСтроки;
			ОбластьСтрокиТаблицы.Параметры.Номенклатура = ВыборкаТовары.Номенклатура;
			ОбластьСтрокиТаблицы.Параметры.Количество = ВыборкаТовары.Количество;
			ОбластьСтрокиТаблицы.Параметры.Цена = ВыборкаТовары.Цена;
			ОбластьСтрокиТаблицы.Параметры.Сумма = ВыборкаТовары.Сумма;

			ИтогТовары = ИтогТовары + ВыборкаТовары.Сумма; 
			ТабличныйДокумент.Вывести(ОбластьСтрокиТаблицы, ВыборкаТовары.Уровень()); 
			
		КонецЦикла;
		
		ВыборкаУслуги = Выборка.Услуги.Выбрать();
		
		Пока ВыборкаУслуги.Следующий() Цикл

			НомерСтроки = НомерСтроки + 1;
			ОбластьСтрокиТаблицы.Параметры.НомерСтроки = НомерСтроки;
			ОбластьСтрокиТаблицы.Параметры.Номенклатура = ВыборкаУслуги.Номенклатура;
			ОбластьСтрокиТаблицы.Параметры.Количество = ВыборкаУслуги.Количество;
			ОбластьСтрокиТаблицы.Параметры.Цена = ВыборкаУслуги.Цена;
			ОбластьСтрокиТаблицы.Параметры.Сумма = ВыборкаУслуги.Сумма;
			
			ИтогУслуги = ИтогУслуги + ВыборкаУслуги.Сумма;
			ТабличныйДокумент.Вывести(ОбластьСтрокиТаблицы, ВыборкаУслуги.Уровень()); 
			
		КонецЦикла;
		
		Итог = ИтогТовары + ИтогУслуги;
		
		ОбластьИтог.Параметры.Итог = Итог;
		ОбластьИтог.Параметры.КоличествоСтрок = НомерСтроки; 
		ОбластьИтог.Параметры.СуммаПрописью = ЧислоПрописью(Итог, "Л=ru_RU;ДП=Истина", "рубль,рубля,рублей,м,копейка,копейки,копеек,ж,2");
		
		// итог
		ТабличныйДокумент.Вывести(ОбластьИтог);     
		
		// подвал
		ТабличныйДокумент.Вывести(ОбластьПодвал); 
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);

	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции 

Функция ВКМ_УбратьНули(Номер) 
    Если Лев(Номер, 1) = "0" Тогда
        Возврат ВКМ_УбратьНули(Прав(Номер, СтрДлина(Номер) - 1)); 
    Иначе
        Возврат Номер;
    КонецЕсли;
КонецФункции
//-ВКМ	_12.08.2024

#КонецОбласти

#КонецЕсли