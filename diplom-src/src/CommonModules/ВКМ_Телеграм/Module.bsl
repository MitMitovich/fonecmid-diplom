
#Область ПрограммныйИнтерфейс

Процедура ОтправитьУведомлениеВТелеграм() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВКМ_УведомленияТелеграмБоту.ВКМ_ТекстСообщения КАК ВКМ_ТекстСообщения,
	               |	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту";

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		Ответ = ОповеститьСпециалистов(Выборка.ВКМ_ТекстСообщения);
		Если Ответ.КодСостояния = 200 Тогда 
			Выборка.Ссылка.ПолучитьОбъект().Удалить();
		КонецЕсли;
	
	КонецЦикла;
			
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ОповеститьСпециалистов(ТекстСообщения);

	Токен =Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	IDГруппы = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	АдресРесурса = "/bot" + Токен + "/sendMessage";
	
	Соединение = Новый HTTPСоединение("api.telegram.org", 443,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	Данные = Новый Структура;
	Данные.Вставить("chat_id", IDГруппы);
	Данные.Вставить("text", ТекстСообщения);
	
	ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку(ПараметрыЗаписи);
	ЗаписатьJSON(Запись, Данные);
	Строка = Запись.Закрыть();
	
	Запрос.УстановитьТелоИзСтроки(Строка);
	Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);
	Если Ответ.КодСостояния <> 200 Тогда
		ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка,,, Ответ.ПолучитьТелоКакСтроку());	
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции
#КонецОбласти